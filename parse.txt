$Program ::= <N> <声明串>
<A> ::= $empty
<N> ::= $empty
<M> ::= $empty
<声明串> ::= <声明>
<声明串> ::= <声明串> <声明>
<声明> ::= $int $ID <声明类型>
<声明> ::= $void $ID <M> <A> <函数声明>
<声明> ::= $int $ID <M> <A> <函数声明>
<声明类型> ::=  $;
<函数声明> ::= $( <形参> $) <语句块>
<形参> ::= <参数列表>
<形参> ::= $void
<参数列表> ::= <参数>
<参数列表> ::= <参数> $, <参数列表>
<参数> ::= $int $ID
<语句块> ::= ${ <内部声明> <语句串> $}
<内部声明> ::= $empty
<内部声明> ::= <内部变量声明> <内部声明>
<内部变量声明> ::= $int $ID $;
<语句串> ::= <语句>
<语句串> ::= <语句> <M> <语句串>
<语句> ::= <if语句>
<语句> ::= <while语句>
<语句> ::= <return语句>
<语句> ::= <assign语句>
<assign语句> ::= $ID $= <表达式> $;
<return语句> ::= $return $;
<return语句> ::= $return <表达式> $;
<while语句> ::= $while <M> $( <表达式> $) <A> <语句块>
<if语句> ::= $if $( <表达式> $) <A> <语句块>
<if语句> ::= $if $( <表达式> $) <A> <语句块> <N> $else <M> <A> <语句块>
<表达式> ::= <加法表达式>
<表达式> ::= <表达式> <比较运算符> <加法表达式>
<比较运算符> ::= $<
<比较运算符> ::= $<=
<比较运算符> ::= $>
<比较运算符> ::= $>=
<比较运算符> ::= $==
<比较运算符> ::= $!=
<加法表达式> ::= <项>
<加法表达式> ::= <项> $+ <加法表达式>
<加法表达式> ::= <项> $- <加法表达式>
<项> ::= <因子>
<项> ::= <因子> $* <项>
<项> ::= <因子> $/ <项>
<因子> ::= $number
<因子> ::= $( <表达式> $)
<因子> ::= $ID $( <实参列表> $)
<因子> ::= $ID
<实参列表> ::= $empty
<实参列表> ::= <表达式>
<实参列表> ::= <表达式> $, <实参列表>